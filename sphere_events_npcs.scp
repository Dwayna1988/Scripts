//****************************************************************************
// SPHERE by : Menasoft ©1997-2007
// www.sphereserver.net
// All SPHERE script files and formats are copyright Menasoft & Partners.
// This file may be freely edited for personal use, but may not be distributed
// in whole or in part, in any format without express written permission from
// Menasoft & Partners.  All donations and contributions
// become the property of Menasoft & Partners.
//****************************************************************************
// FILE LAST UPDATED: Tuesday, October 16, 2013
//
VERSION=0.56c


///////////////////////////
// Some Useful Functions //
///////////////////////////

[FUNCTION f_ishuman]
IF ( (<OBODY>==c_man) || (<OBODY>==c_woman) )
   RETURN 1
ELSE
   RETURN 0
ENDIF

[FUNCTION f_iself]
IF ( (<OBODY>==c_elf_male) || (<OBODY>==c_elf_female) )
   RETURN 1
ELSE
   RETURN 0
ENDIF

[FUNCTION f_isgarg]
IF ( (<OBODY>==c_gargoyle_male) || (<OBODY>==c_gargoyle_female) )
   RETURN 1
ELSE
   RETURN 0
ENDIF

[FUNCTION f_isfemale]
IF ( (<OBODY>==c_woman) || (<OBODY>==c_elf_female) || (<OBODY>==c_gargoyle_female) )
   RETURN 1
ELSE
   RETURN 0
ENDIF

////////////
// Events //
////////////

[EVENTS e_Undead]
ON=@EnvironChange
	Return 0
	// Enter "//" before Return 0, if you want to make script working
	// i like the dark
	if (<sector.isdark>) || (<flags>& (statf_nightsight|statf_indoors))
		if (!<tag0.lightstr>)
			return 0
		endif
		anim 011
		bark 4
		str=<tag.lightstr>	// restore me.
		hits=<str>
		tag.lightstr=
		karma=<tag0.lightkarma>
		tag.lightkarma=//just dispose of this.
		fame=<tag0.lightfame>
		tag.lightfame=
		flags ^= statf_conjured	// no loot if killed.
		return 0
	endif
	// i can't live in light areas. weaken me ? or destroy me ?
	anim 014
	bark 4
	if (<tag0.lightstr>)
		return 0
	endif
	if ( <flags>&statf_conjured	) 
		remove
		return 1
	endif
	flags |= statf_conjured	// no loot if killed.
	tag.lightstr=<str>
	str=1
	hits=1	// very weak.
	tag.lightkarma=<karma>	// killing it means nothing now.
	karma=-2
	tag.lightfame=<fame>
	fame=10
	return 0

[EVENTS e_Carnivores]
// weak carnivores.

[EVENTS e_Carnivores2]
// normal carnivores.

[EVENTS e_Carnivores3]
// strong carnivores.

[EVENTS e_Terathan]

[EVENTS e_Ophidian]

[EVENTS e_Horses]

[EVENTS e_Notoriety]

[EVENTS e_Ostards]

[EVENTS e_Minotaurs]

[EVENTS e_poisonbite]
//Poison bite attack
ON=@Hit
   IF !(<SRC.FLAGS> & statf_poisoned)
      SRC.SPELLEFFECT s_poison,<POISONING>,1,<UID>
//      SRC.EFFECT 3,i_fx_sparkle,1,16,0,044
//      SRC.SOUND snd_spell_poison
   ENDIF

[EVENTS e_poisoned_spit]
//Poisoned Spit Defense
ON=@GetHit
   REF12=<SRC>
   IF (<MANA> > 15) && (<DISTANCE> > 2) && !(<SRC.FLAGS> & statf_poisoned) && (<SRC> != <UID>)
      MANA -= 15
      TRYSRC <UID> REF12.EFFECT 0,i_fx_fireball,10,16,0,044,4
      TRYSRC <UID> REF12.SPELLEFFECT s_poison,<POISONING>,0//FIXME:resist
      REF12.DAMAGE <R15,25> <DEF.dam_poison> <UID>
      REF12.SOUND snd_spell_poison
      REF12.SYSMESSAGELOC 1070821,<NAME>//"~ spits a poisonous substance at you!"
   ENDIF

[EVENTS e_solen_spit]
//Acidic Spit Defense
ON=@GetHit
   REF12=<SRC>
   IF (<MANA> > 15) && (<DISTANCE> > 2) && (<SRC> != <UID>)
      MANA -= 15
      TRYSRC <UID> REF12.EFFECT 0,i_fx_fireball,10,16,0,044,4
      REF12.DAMAGE <R15,25> <DEF.dam_acidic> <UID>
      REF12.SYSMESSAGELOC 1071966//"The solen's damaged acid sac squirts acid!"
      REF12.SOUND snd_spell_poison
      SERV.NEWITEM i_pool_acid
      NEW.P=<REF12.P>
      NEW.ATTR=attr_decay|attr_move_never
      NEW.TIMER={5 10}
      NEW.LINK=<UID>
   ENDIF

[ITEMDEF i_pool_acid]
ID=021c
NAME=acid pool
TYPE=t_normal
CATEGORY=Monster
SUBSECTION=Attack
DESCRIPTION=Acid Pool
ON=@Create
   ATTR=attr_decay|attr_move_never
   TIMER={4 8}
ON=@Step
   IF (<SRC.ISPLAYER>)
      SRC.DAMAGE <R10,15> <DEF.dam_acidic> <LINK>
   ENDIF

[EVENTS e_bleed_attack]
ON=@Hit
   //Bleed attack
   IF (<MANA> >= 25)&&(RAND(10)==1)
      MANA -= 25
      SRC.NEWITEM=i_bleed_timer
      NEW.LINK=<UID>
      SRC.EQUIP <NEW>
   ENDIF

[ITEMDEF i_bleed_timer]
//1060202//"*Bleeding*"
ID=0122a
NAME=bleed timer
TYPE=t_eq_script
LAYER=30
WEIGHT=0
ON=@Create
   ATTR=attr_decay|attr_can_decay
   MORE1=6
ON=@Equip
   ATTR=attr_decay|attr_can_decay
   TIMER=2
ON=@Timer
   IF (<CONT>)
      IF !(<CONT.HITS>)
         MORE1=0
      ELSE
         MORE1 -= 1
      ENDIF
      DOSWITCH <MORE1>
         BEGIN // MORE1=0
            CONT.SYSMESSAGELOC 1060167//"The bleeding wounds have healed, you are no longer bleeding!"
            REMOVE
         END
         BEGIN // MORE1=1
            CONT.EMOTE bleed slowly
            CONT.DAMAGE {1 2} <DEF.dam_god> <LINK>
            TIMER=2
         END
         BEGIN // MORE1=2
            CONT.EMOTE bleed slowly
            CONT.DAMAGE {2 4} <DEF.dam_god> <LINK>
            TIMER=2
         END
         BEGIN // MORE1=3
            CONT.EMOTE bleed moderately
            CONT.DAMAGE {3 6} <DEF.dam_god> <LINK>
            TIMER=2
         END
         BEGIN // MORE1=4
            CONT.EMOTE bleed heavily
            CONT.DAMAGE {4 9} <DEF.dam_god> <LINK>
            TIMER=2
         END
         BEGIN // MORE1=5
            CONT.EMOTE bleed profusely
            CONT.DAMAGE {5 12} <DEF.dam_god> <LINK>
            TIMER=2
         END
      ENDDO
   ENDIF
   RETURN 1

[ITEMDEF i_summon_mem]
ID=i_rune_summon_creature
NAME=custom summon memory
TYPE=t_spell
LAYER=41
WEIGHT=0
ON=@Create
   MOREX=40
   MOREY=900
   TIMER=20
   ATTR=026

[FUNCTION f_NPCRemovalTimer]
SERV.NEWITEM i_removetimer_mem
EQUIP <NEW>
NEW.TIMER=<ARGN1>

[ITEMDEF i_removetimer_mem]
ID=i_memory
NAME=remove timer
TYPE=t_eq_script
ON=@Timer
   CONT.REMOVE
   RETURN 1

[EVENTS e_trainer]
ON=@ContextMenuRequest
   IF (<NPC> < 2) || (<NPC> == 4) || (<NPC> > 9)
      // Not all brain types can train
      SERV.LOG ERROR: <NAME> (<UID>) has the e_trainer event, but their brain type is not supported
   ELSE
      FOR x 0 57
         LOCAL.PlayerSkill=<SRC.<SERV.SKILL.<dLOCAL.x>.KEY>>
         LOCAL.NpcSkill=<<SERV.SKILL.<dLOCAL.x>.KEY>>
         IF (<LOCAL.NpcSkill> > 30.0)
            IF (<LOCAL.x>!=54) //Spellweaving cannot be trained
               SRC.ADDCONTEXTENTRY <EVAL 200+<LOCAL.x>>,<EVAL 6000+<LOCAL.x>>,<EVAL <QVAL <LOCAL.PlayerSkill> < <LOCAL.NpcSkill> ? 0 : 1>>,1
            ENDIF
         ENDIF
      ENDFOR
   ENDIF
ON=@ContextMenuSelect
   IF (<ARGN> >= 200)
      LOCAL.Skill=<EVAL <ARGN>-200>
      LOCAL.PlayerSkill=<SRC.<SERV.SKILL.<dLOCAL.Skill>.KEY>>
      LOCAL.NpcSkill=<<SERV.SKILL.<dLOCAL.Skill>.KEY>>
      TRAIN <SERV.SKILL.<dLOCAL.Skill>.KEY>
   ENDIF

[ITEMDEF i_mem_vicious_bite]
ID=i_pet_kepetch
NAME=Vicious bite memory
TYPE=t_eq_script
LAYER=layer_special
WEIGHT=0
ON=@Create
   ATTR=attr_decay|attr_can_decay
   TIMER=2
ON=@Timer
   IF (<MORE1> > 0)
      MORE1 -= 1
      CONT.DAMAGE 5 <DEF.dam_poison> <LINK>
      CONT.SYSMESSAGELOC 1112473//"Your vicious wound is festering!"
      TIMER=20
      RETURN 1
   ENDIF

[EVENTS e_angry_fire]

[EOF]
