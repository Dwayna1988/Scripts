[SPEECH spk_guildspeech]
ON=I resign from my guild
IF ((<ARGN1> == <DEF.TALKMODE_SAY>) && <GUILD>)
    f_guildsys_removecharfromguild <guild.uid>,<uid>
    RETURN 6
ENDIF
RETURN 1

ON=*
IF (<ARGN1> != <DEF.TALKMODE_GUILD>)
    RETURN 0 // Not my problem
ENDIF

IF ( !<DEF0.GUILDCFGSPEECH_GLOBAL_ENABLE> )
    RETURN 1
ENDIF

IF ( !<GUILD> )
    RETURN 1
ENDIF

LOCAL.PRIVSPEECH = <GUILD.MEMBERFROMUID.<UID>.PRIV>
IF (<LOCAL.PRIVSPEECH> == <DEF.STONEPRIV_ACCEPTED>)
    LOCAL.PRIVSPEECH = <DEF.STONEPRIV_CANDIDATE>
ENDIF

IF ((<LOCAL.PRIVSPEECH> == <DEF.STONEPRIV_CANDIDATE>) && !<DEF0.GUILDCFGSPEECH_ENABLE_CANDIDATES>)
    RETURN 1
ENDIF

IF ((<LOCAL.PRIVSPEECH> == <DEF.STONEPRIV_MEMBER>) && !<DEF0.GUILDCFGSPEECH_ENABLE_MEMBERS>)
    RETURN 1
ENDIF

IF (<CTAG0.GUILDSYS_LASTGUILDMSG_TIME> < <SERV.TIME>)
    CTAG.GUILDSYS_LASTGUILDMSG_TIME = <SERV.TIME> + <DEF0.GUILDCFGSPEECH_SPAM_INTERVAL>
    f_guildsys_speech_sendsmsg <guild.uid>,<args>
ENDIF

RETURN 1

[EOF]